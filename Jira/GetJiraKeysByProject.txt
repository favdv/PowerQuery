/*
This Power Query function is used to retrieve JIRA keys by project. This does not work without amendments due to the way API calls are handled. It is set up, so auto-refresh will work. Auto refresh typically fails due to the domain part in Web Contents being dynamic, which is not allowed. Auto-refresh also does not work if you add the if statement within Web.Contents, if you use a parameter or if you reference the column directly within Web.Contents.

While you could retrieve any number of fields with this function, JIRA throttles the amount of data being returned if you request all data. To limit this, you could use this function to retrieve the keys and subsequently retrieve all fields for the specific key (or list of keys).

To implement this, either:
- Add a blank query in PowerBI
- Copy this file into the blank Query
- Rename the blank Query to whatever you want to call the function
- In the table where you want to use it, call the function via <newstep> = <function name>(<previous step>,"<Domain column>","<project column")
Or: 
- Open the table where you want to use the function
- the Get Keys step as a new step and change SourceTable to the table containing the columns, change Domains to the name of the domain column (as a string) and Projects to the name of the Project Id column (as a string) 

In any case, also change <your domain> to the domain name of your JIRA tenant. If the organisation has more than one domain, you can add more by extending the code by ading more if statements, like 

if Record.Field(_,Domains) = "<your domain>"
then Json.Document(
  Web.Contents(
    "<your domain>",
    [
      RelativePath = "rest/api/3/search/",
      Query = [
        jql="project="&Record.Field(_,Projects),
        maxResults = "10000" 
      ]
    ]
  )
) else 
if Record.Field(_,Domains) = "<your 2nd domain>"
then Json.Document(
  Web.Contents(
    "<your 2nd domain>",
    [
      RelativePath = "rest/api/3/search/",
      Query = [
        jql="project="&Record.Field(_,Projects),
        maxResults = "10000" 
      ]
    ]
  )
)
else null
*/

(SourceTable as table, Domains as text, Projects as text)=> 
let
  #"Get Keys" = Table.ExpandRecordColumn(
		  Table.ExpandListColumn(
		    Table.ExpandRecordColumn(
		      Table.AddColumn(SourceTable, "keys", each 
            if Record.Field(_,Domains) = "<your domain>" 
            then Json.Document(
              Web.Contents(
                "<your domain>",
                  [
                    RelativePath = "rest/api/3/search/",
                    Query = [
                      jql="project="&Record.Field(_,Projects),
                      maxResults = "10000" 
                    ]
                  ]
              )
            )
            else null
          ), 
		      "keys", {"issues"}
		    ), "issues"
		  ), 
		  "issues", {"key"}
	  )
in #"Get Keys"
